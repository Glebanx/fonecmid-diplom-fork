
&НаКлиенте
Процедура СоздатьДокументыРеализации(Команда)
	
	Период = Объект.ВКМ_Период;
	
	Если НЕ ЗначениеЗаполнено(Период) Тогда 
		ОбщегоНазначенияКлиент.СообщитьПользователю("Не заполнен период");
		Возврат;
	КонецЕсли;
	
	ДлительнаяОперация = СоздатьДокументыРеализацииНаСервере(); 
	
	Оповещение = Новый ОписаниеОповещения("СоздатьДокументыРеализацииЗавершение", ЭтотОбъект);
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Оповещение);
КонецПроцедуры


&НаСервере
Функция СоздатьДокументыРеализацииНаСервере()

	ИмяМетода = "Обработки.ВКМ_МассовоеСозданиеАктов.СоздатьДокументыРеализацииНаСервере";
	
	Возврат ДлительныеОперации.ВыполнитьФункцию(УникальныйИдентификатор, ИмяМетода, Объект.ВКМ_Период);

КонецФункции 

&НаКлиенте
Процедура СоздатьДокументыРеализацииЗавершение(Результат, ДополнительныеПараметры) Экспорт 
	
	МассивТЧ = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
	ЗаполнитьТЧ(МассивТЧ);
	Сообщить("Создание документов завершено");
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТЧ(МассивТЧ)
	
	ТЧ = Объект.ВКМ_ДоговорыИРеализации;
	ТЧ.Очистить();
	
	Для Каждого Элемент Из МассивТЧ Цикл 
		
		Обработки.ВКМ_МассовоеСозданиеАктов.ЗаполнитьТЧ(Элемент.Договор, Элемент.Реализация, ТЧ); 
		
	КонецЦикла;

КонецПроцедуры